# Silo\silo_packet_requester.ic10
alias Mirror d0
alias DialItem d1
alias DialQuantity d2
alias Button d3
alias Display d4
# Constant
define QUERY_FREE 9000000
define QUERY_ITEM 9000101
define QUERY_QUANTITY 9000102
define PACKET_SIZE 12
define QTY 100
define QMIN 800
define QMAX 1600
define START_SP 1 # 1,7:Ores 13,19,25:Ingots
define COLOR_1 10 # Pink
define COLOR_2 1 # Gray
# Alias Register
alias Device r0
alias Action r1
alias Choose r2
alias LastChoose r3
alias Qty r4
alias Hash r5 # Ingot Hash
alias LastHash r6 # Ingot Hash
alias Clock r7
alias Stage r9
alias DColor r13
alias Provider r14
alias Temp r15
# Settings
s DialItem Mode 10
s DialQuantity Mode 10
move Stage 0
start:
yield
s Display Color 2 # Green
l Action Button Setting
l Choose DialItem Setting
jal get_hash
s db Setting Hash
bneal Hash LastHash pre_request_qty
beqal Action 1 request_item
jal check_stage
jal update_quantity
j start

check_stage:
beq Stage 0 ra
l Temp d0:0 Channel1
bne Temp 0 ra
move Stage 0
j request_qty

pre_request_qty:
breq Choose LastChoose 3
move Clock 2
move LastChoose Choose
beq Clock 0 request_qty # launch request
sub Clock Clock 1
j start

request_qty:
l Temp Mirror Setting # Read Memory Provider via mirror
brne Temp QUERY_FREE -1 # wait response
move LastHash Hash
s Display Color COLOR_1
s d0:0 Channel0 Hash # Write Hash
s d0:0 Channel1 ninf # Reset Quantity
s Mirror Setting QUERY_QUANTITY # send quantity request
j ra

update_quantity:
l Temp d0:0 Channel1
s Display Setting Temp
j ra

request_item:
l Qty DialQuantity Setting
mul Qty Qty QTY
l Temp Mirror Setting # Read Memory Provider via mirror
brne Temp QUERY_FREE -1 # wait response
s d0:0 Channel0 Hash # Write Hash
s d0:0 Channel1 Qty # Write Quantity
s d0:0 Channel2 1 # Write Rootage
s Mirror Setting QUERY_ITEM # Write Memory Provider via mirror
move Stage 1
j ra

get_hash:
move sp Choose
add sp sp START_SP
pop Hash
j ra