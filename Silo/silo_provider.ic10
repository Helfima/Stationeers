# silo_provider.ic10
alias Silo0 d0
alias Silo1 d1
alias Silo2 d2
alias Silo3 d3
alias Silo4 d4
alias Silo5 d5
#Constant
define TYPE_SILO 1155865682 # Silo Hash
define TYPE_STACKER1 -2020231820 # Stacker Hash
define TYPE_STACKER2 1585641623 # Stacker Hash
define STACK 100
define WAIT 0
define NONE -3
define START_SP 1 # 1,7:Ores 13,19,25:Ingots
# Alias Register
alias Silo r0
alias LastSilo r1
alias Request r2
alias AbsRequest r3
alias Qty r4
alias Hash r5
#Settings
jal push_hash_ingot
sb TYPE_SILO On 1
sb TYPE_STACKER1 On 1
sb TYPE_STACKER1 Setting STACK
sb TYPE_STACKER2 On 1
sb TYPE_STACKER2 Setting STACK
start:
yield
l Request db Setting
beq Request WAIT start # Wait Request
abs AbsRequest Request
bgt AbsRequest 1000000 request_read # Request Hash
jal read_silo
s db Setting Qty
j start
request_read:
jal check_hash
bne Silo NONE found_silo # Found Silo
move LastSilo Silo
move Qty 0 # Reset Quantity
s db Setting Qty # End Request
j start
found_silo:
beq Silo LastSilo request_open # Open Request
# Quantity
move LastSilo Silo
jal read_silo
j start
request_open:
jal action_silo
jal read_silo
j start

read_silo:
move Qty 0
beq LastSilo NONE ra
bdns rr1 ra # if no device exit
l Qty rr1 Quantity
mul Qty Qty STACK
s db Setting Qty # Quantity Request
j ra

action_silo:
beq Silo NONE ra
bdns rr0 ra # if no device exit
s rr0 Open 1
sleep 1
s rr0 Open 0
j ra

check_hash:
move Silo 0
loop_hash:
add sp Silo START_SP
peek Hash
beqal Request Hash ra
add Silo Silo 1
ble Silo 5 loop_hash
move Silo NONE
j ra

push_hash_ingot:
move sp 0
# Ores
push 1724793494  # 1:Coal Ore
push -983091249  # 2:Colbalt Ore
push -707307845  # 3:Copper Ore
push -1348105509 # 4:Gold Ore
push 1758427767  # 5:Iron Ore
push -190236170  # 6:Lead Ore

push 1830218956  # 7:Nickel Ore
push 1103972403  # 8:Silicon Ore
push -916518678  # 9:Silver Ore
push -1516581844 # 10:Uranium Ore
push -831480639  # 11:Biomass
push -654790771  # 12:Steel Ingot

#Ingots
push 2134647745  # 13:Lead Ingot
push 252561409   # 14:Charcoal
push -404336834  # 15:Copper Ingot
push -929742000  # 16:Silver Ingot
push 226410516   # 17:Gold Ingot
push -1301215609 # 18:Iron Ingot

push -1406385572 # 19:Nickel Ingot
push -290196476  # 20:Silicon Ingot
push -654790771  # 21:Steel Ingot
push 502280180   # 22:Electrum Ingot
push -82508479   # 23:Solder Ingot
push 1058547521  # 24:Constantan Ingot

push -297990285  # 25:Invar Ingot
push 412924554   # 26:Astroloy Ingot
push -787796599  # 27:Inconel Ingot
push 156348098   # 28:Waspaloy Ingot
push 1579842814  # 29:Hastelloy Ingot
push -1897868623 # 30:Stellite Ingot
j ra