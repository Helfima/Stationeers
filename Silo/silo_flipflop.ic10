# Silo\silo_flipflop.ic10
alias FlipFlop0 d0
alias FlipFlop1 d1
alias FlipFlop2 d2
alias FlipFlop3 d3
alias FlipFlop4 d4
alias FlipFlop5 d5
#Constant
define TYPE_SILO 1155865682 # Silo Hash
define TYPE_FLIPFLOP_RIGHT 163728359 # FlipFlop Hash
define TYPE_FLIPFLOP_LEFT -810874728 # FlipFlop Hash
define TYPE_STACKER1 -2020231820 # Stacker Hash
define TYPE_STACKER2 1585641623 # Stacker Hash
define STACK_ORE 50
define STACK_INGOT 100
define STORAGE_LINE 1 # 1,2:Ores 3,4,5:Ingots
# Alias Register
alias FlipFlop r0
alias Request r1
alias Hash r2
alias Index r3
alias Stack r6
alias StartSp r13
alias Temp r15
#Settings
sle Temp STORAGE_LINE 2
select Stack Temp STACK_ORE STACK_INGOT
mul StartSp STORAGE_LINE 6
sub StartSp StartSp 5 # 1,7:Ores 13,19,25:Ingots
sb TYPE_FLIPFLOP_RIGHT On 0
sb TYPE_FLIPFLOP_RIGHT Setting 0
sb TYPE_FLIPFLOP_RIGHT SettingOutput 0
sb TYPE_FLIPFLOP_LEFT On 0
sb TYPE_FLIPFLOP_LEFT Setting 0
sb TYPE_FLIPFLOP_LEFT SettingOutput 0
sb TYPE_SILO On 1
sb TYPE_STACKER1 Setting Stack
sb TYPE_STACKER1 On 1
sb TYPE_STACKER2 Setting Stack
sb TYPE_STACKER2 On 1
start:
yield
move r0 0
loop_device:
s dr0 On 0
beqal r0 0 check_input_row
bneal r0 0 check_input_silo
add r0 r0 1
ble r0 5 loop_device
j start

check_input_silo:
ls Request dr0 0 PrefabHash
beq Request 0 ra
sub sp r0 1
add sp sp StartSp
peek Hash
beq Request Hash silo_way
s dr0 Mode 0 # FlipFlop Way:Exit
s dr0 On 1
j ra
silo_way:
s dr0 Mode 1 # FlipFlop Way:Silo
s dr0 On 1
j ra

check_input_row:
ls Request dr0 0 PrefabHash
beq Request 0 ra
move Index 0
loop_hash:
add sp Index StartSp
peek Hash
beq Request Hash row_way
add Index Index 1
ble Index 5 loop_hash
s dr0 Mode 0 # FlipFlop Way:Exit
s dr0 On 1
j ra
row_way:
s dr0 Mode 1 # FlipFlop Way:Silo Row
s dr0 On 1
j ra
