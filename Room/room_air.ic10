# Air Room Controller
alias Sensor d0
alias PumpO2 d1
alias AnalyserO2 d2
alias PumpN2 d3
alias AnalyserN2 d4
alias PumpWaste d5
# Alias Register
alias Pcur r0 # Current Pressure
alias Tcur r1 # Current Temperature
alias O2 r2
alias N2 r3
alias IsWaste r4
alias IsTemp r5
alias IsPercent r7
alias DeviceOn r8
alias Tmax r14
alias Temp r15
# Constant
define KELVIN 273
define TMAX 40
define PMIN 50
define PMAX 100
define PERCENT_N2 0.80
define PERCENT_O2 0.20
define DELTA 0.01
# Settings
add Tmax TMAX KELVIN
s PumpO2 Setting 10
s PumpN2 Setting 10
s PumpWaste Setting 100
start:
yield
l Pcur Sensor Pressure
l O2 Sensor RatioOxygen
l N2 Sensor RatioNitrogen
jal checkWaste
jal checkO2
jal checkN2
j start

checkO2:
l Tcur AnalyserO2 Temperature
slt IsTemp Tcur Tmax
sub Temp O2 PERCENT_O2
sltz IsPercent Temp
nor DeviceOn IsWaste IsWaste
and DeviceOn DeviceOn IsPercent
and DeviceOn DeviceOn IsTemp
s PumpO2 On DeviceOn
j ra

checkN2:
l Tcur AnalyserN2 Temperature
slt IsTemp Tcur Tmax
sub Temp N2 PERCENT_N2
sltz IsPercent Temp
nor DeviceOn IsWaste IsWaste
and DeviceOn DeviceOn IsPercent
and DeviceOn DeviceOn IsTemp
s db Setting N2
s PumpN2 On DeviceOn
j ra

checkWaste:
brge Pcur PMIN 2
move IsWaste 0
brle Pcur PMAX 2
move IsWaste 1
s PumpWaste On IsWaste
j ra