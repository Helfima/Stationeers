# Hydroponic\tray_light.ic10
alias Tray0 d0
alias DisplayMin d1
alias DisplaySec d2
alias DisplayEff d3
alias DisplayMat d4
alias DisplayTim d5
# Constant
define TYPE_LIGHT -1758710260
define DAY 20 # 20 min
define DELAY 300
# Alias Register
alias IsOn r0
alias IsOccupied r1
alias DarkTime r2
alias LastEfficiency r3
alias Err r4
alias Time r5
alias IsModel r6
alias Seed r7
alias Light r8
alias Darkness r9
alias TimeGrowth r10
alias Hash r11
alias Min r12
alias Sec r13
alias Clock r14
alias Temp r15
# Settings
move LastEfficiency 0
move Time 0
move IsModel 0
start:
yield
#s db Setting Darkness
ls IsOccupied Tray0 0 Occupied
jal read_model
bdseal DisplayMin check_clock
bdseal DisplayMat check_maturity
bdseal DisplayEff check_efficiency
bdseal DisplayTim check_timer
jal check_light
sub Time Time 1
bge Time 0 start # wait timer
mul Time DELAY 2 # reset timer in second
j start

check_light:
mul DarkTime Darkness DELAY
#add Temp Light Darkness
div DarkTime DarkTime DAY
s db Setting DarkTime
mul DarkTime DarkTime 2
sgt IsOn Time DarkTime # on if not darkness
sgt Temp Light 0
select IsOn Temp IsOn 0 # only dark
sgt Temp Darkness 0
select IsOn Temp IsOn 1 # only light
select IsOn IsOccupied IsOn 0 # turn off
sb TYPE_LIGHT On IsOn
j ra

check_clock:
s DisplayMin On IsOccupied
s DisplaySec On IsOccupied
sub Clock Clock 0.5
select Clock IsOccupied Clock TimeGrowth # reset clock
div Sec Clock 1
div Min Sec 60
trunc Min Min
s DisplayMin Setting Min
s DisplayMin Color 10
mul Min Min 60
sub Sec Sec Min
trunc Temp Sec
s DisplaySec Setting Temp
seq Temp Temp Sec
select Temp Temp 10 11
s DisplaySec Color Temp
j ra

check_maturity:
s DisplayMat On IsOccupied
ls Temp Tray0 0 Growth
s DisplayMat Setting Temp
s DisplayMat Color 5
j ra

check_efficiency:
s DisplayEff On IsOccupied
ls Temp Tray0 0 Efficiency
s DisplayEff Setting Temp
slt Temp LastEfficiency Temp
select Temp Temp 4 2
s DisplayEff Color Temp
move LastEfficiency Temp
j ra

check_timer:
s DisplayTim On IsOccupied
div Temp Time 2
trunc Temp Temp
s DisplayTim Setting Temp
s DisplayTim Color 1
j ra

read_model:
ls IsOccupied Tray0 0 Occupied # Tray Occupied
beq IsOccupied 0 reset_model # Empty
beq IsModel 1 ra # already
ls Hash Tray0 0 PrefabHash # Tray Hash
move sp -4
loop_model:
add sp sp 5 # decale index
peek Temp
bgt sp 120 ra # exit
bne Temp Hash loop_model
add sp sp 4
pop TimeGrowth
pop Darkness
pop Light
pop Seed
move IsModel 1
move Clock TimeGrowth
j ra
reset_model:
move IsModel 0
j ra