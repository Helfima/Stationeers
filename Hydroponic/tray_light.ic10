# Hydroponic\tray_light.ic10
alias DisplayMin d1
alias DisplaySec d2
alias DisplayEff d3
alias DisplayMat d4
alias DisplayTim d5
# Constant
define TYPE_LIGHT -1758710260
define TYPE_TRAY -1841632400
define DELAY 300
# Alias Register
alias IsOn r0
alias IsOccupied r1
alias DarkTime r2
alias LastEfficiency r3
alias Clock r5
alias IsModel r6
alias Seed r7
alias Light r8
alias Darkness r9
alias TimeGrowth r10
alias Hash r11
alias Min r12
alias Sec r13
alias Timer r14
alias Temp r15
# Settings
move LastEfficiency 0
move Timer 0
jal reset_model
start:
yield
s db Setting DarkTime
lbs IsOccupied TYPE_TRAY 0 Occupied 3 # 0:Average 1:Sum 2:Min 3:Max
jal read_model
bdseal DisplayMin check_clock
bdseal DisplayMat check_maturity
bdseal DisplayEff check_efficiency
bdseal DisplayTim check_timer
jal check_light
sub Timer Timer 1
bge Timer 0 start # wait timer
mul Timer DELAY 2 # reset timer in second
j start

check_light:
add DarkTime Darkness Light
div DarkTime Darkness DarkTime
mul DarkTime DarkTime DELAY
mul DarkTime DarkTime 2
sgt IsOn Timer DarkTime # on if not darkness
sgt Temp Light 0
select IsOn Temp IsOn 0 # only dark
sgt Temp Darkness 0
select IsOn Temp IsOn 1 # only light
select IsOn IsOccupied IsOn 0 # turn off
sb TYPE_LIGHT On IsOn
j ra
check_clock:
s DisplayMin On IsOccupied
s DisplaySec On IsOccupied
sub Clock Clock 0.5
select Clock IsOccupied Clock TimeGrowth # reset clock
div Sec Clock 1
div Min Sec 60
trunc Min Min
s DisplayMin Setting Min
s DisplayMin Color 10
mul Min Min 60
sub Sec Sec Min
trunc Temp Sec
s DisplaySec Setting Temp
seq Temp Temp Sec
select Temp Temp 10 11
s DisplaySec Color Temp
j ra
check_maturity:
s DisplayMat On IsOccupied
lbs Temp TYPE_TRAY 0 Growth 3 # 0:Average 1:Sum 2:Min 3:Max
s DisplayMat Setting Temp
s DisplayMat Color 5
j ra
check_efficiency:
s DisplayEff On IsOccupied
lbs Temp TYPE_TRAY 0 Efficiency 3 # 0:Average 1:Sum 2:Min 3:Max
s DisplayEff Setting Temp
slt Temp LastEfficiency Temp
select Temp Temp 4 2
s DisplayEff Color Temp
move LastEfficiency Temp
j ra
check_timer:
s DisplayTim On IsOccupied
div Temp Timer 2
trunc Temp Temp
s DisplayTim Setting Temp
s DisplayTim Color 1
j ra
read_model:
beq IsOccupied 0 reset_model # Empty
beq IsModel 1 ra # already
lbs Hash TYPE_TRAY 0 PrefabHash 3 # 0:Average 1:Sum 2:Min 3:Max
brne Hash 0 2 # bpypass if ok, if negative value need load in min mode
lbs Hash TYPE_TRAY 0 PrefabHash 2 # 0:Average 1:Sum 2:Min 3:Max
move sp -4
loop_model:
add sp sp 5 # decale index
peek Temp
bgt sp 160 ra # exit 32 plants x 5 lines
bne Temp Hash loop_model
put db 255 sp
add sp sp 4
pop TimeGrowth
pop Darkness
pop Light
pop Seed
move IsModel 1
move Clock TimeGrowth
j ra
reset_model:
move IsModel 0
put db 255 -1
j ra