# Hydroponic Vending Controller
alias Vending d0
alias ReaderOccupied d1
alias ReaderMature d2
alias SorterHarvie d3
alias SorterReturn d4
alias StakerInput d5
# Constant
define TYPE_HARVIE 958056199
define DAMAGE_LIMIT 0.9
define Limit 8 # 8 plants
# Alias Register
alias Count r0
alias Slot r1
alias IsReturn r2
alias IsOccupied r3
alias IsMature r4
alias IsRequest r5
alias LastDamage r6
alias ReturnDamage r7
alias Hash r8
alias Timer r9
alias Temp r10
#settings
move IsRequest 0
move Slot 2
move Timer -1
s StakerInput Mode 1
s SorterReturn Mode 2
s SorterHarvie Mode 2
s SorterHarvie ClearMemory 1
sb TYPE_HARVIE ClearMemory 1
start:
s db Setting LastDamage
yield
l Count SorterHarvie ExportCount
l IsOccupied ReaderOccupied Setting
l IsMature ReaderMature Setting
jal functionInput
jal functionReturn
jal functionHarvie
jal functionPlant
jal functionHarvest
jal functionVending
j start
functionInput:
s db Setting -10
yield
l Temp StakerInput Output
bne Temp -1 ra
ls Temp StakerInput 2 Occupied # import:0 export:1 processing:2
beq Temp 0 ra # Empty
brne Timer -1 2
move Timer 120 # Init Timer
sub Timer Timer 1
bne Timer 0 ra # Not ready
s StakerInput Activate 1
move Timer -1
j ra
functionReturn:
s db Setting -20
yield
l Temp SorterReturn Output
bne Temp -1 ra
ls Temp SorterReturn 0 Occupied
beq Temp 0 ra # Empty
beq IsRequest 0 normalWay
ls Temp SorterReturn 0 Damage
bge Temp ReturnDamage normalWay
s Vending RequestHash Hash
returnWay:
s SorterReturn Output 1 # Way:Return
j ra
normalWay:
s SorterReturn Output 0 # Way:Normal
j ra
functionHarvie:
s db Setting -30
yield
l Temp SorterHarvie Output
bne Temp -1 ra
ls Temp SorterHarvie 0 Occupied
beq Temp 0 ra # Empty
breq IsOccupied 1 4
brge Count Limit 3
s SorterHarvie Output 1 # Way:Harvie
j ra
s SorterHarvie Output 0 # Way:Waste
j ra
functionPlant:
lb Temp TYPE_HARVIE ImportCount 1 # 0:Average 1:Sum 2:Min 3:Max
beq Temp 0 ra # Empty
min Count Count 8 # Count Cap
blt Temp Count ra # Not Ready
sb TYPE_HARVIE Plant 1
sleep 1
sb TYPE_HARVIE ClearMemory 1
move IsRequest 0
j ra
functionHarvest:
beq IsMature 0 ra # Not Mature
sleep 1
sb TYPE_HARVIE Harvest 1
s SorterHarvie ClearMemory 1
j ra
functionVending:
brlt Slot 102 2
move Slot 2 # Reset Slot
loop:
ls LastDamage Vending Slot Damage
bgt LastDamage DAMAGE_LIMIT exit
add Slot Slot 1
brlt Slot 102 2
beq LastDamage 0 loop
j ra
exit:
beq IsOccupied 1 ra
beq IsRequest 1 ra
ls Hash Vending Slot PrefabHash
s Vending RequestHash Hash
move IsRequest 1
move Slot 2 # Reset Slot
move ReturnDamage LastDamage
j ra
