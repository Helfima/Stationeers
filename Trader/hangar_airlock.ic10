# Trader\hangar_airlock.ic10
# Constant
define TYPE_LEVER 1220484876
define TYPE_SENSOR -1252983604
define TYPE_LED 1944485013
define TYPE_DISPLAY -815193061 # Small Display
define DISP_1 HASH("Display 1")
define DISP_2 HASH("Display 2")
define TYPE_GATE_MEDIUM -566348148
define TYPE_GATE_LARGE -1351081801
define TYPE_VENT -785498334
define VENT_EXTERNAL HASH("Powered Vent External")
define VENT_INTERNAL HASH("Powered Vent Internal")
define TYPE_TANK 1280378227
define TANK_INT HASH("Tank Internal")
define TANK_EXT HASH("Tank External")
define PMIN 0
define PTANK_INT 10
define PTANK_EXT 500
# Alias Register
alias Request r0
alias Pcur r1
alias Tcur r2
alias IsOn r3
alias IsOpen r4
alias IsIdle r5
alias IsOxygen r6
alias PTInt r7
alias PTExt r8
alias Closed r13
alias Sequence r14
alias Temp r15
# Settings
sb TYPE_VENT On 0
lb IsOpen TYPE_GATE_MEDIUM Open 3 # 0:Average 1:Sum 2:Min 3:Max
lb Temp TYPE_GATE_LARGE Open 3 # 0:Average 1:Sum 2:Min 3:Max
or IsOpen IsOpen Temp
select Sequence IsOpen 1 5
start:
yield
s db Setting Sequence
lb Request TYPE_LEVER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
lb Pcur TYPE_SENSOR Pressure 3 # 0:Average 1:Sum 2:Min 3:Max
lb Tcur TYPE_SENSOR Temperature 3 # 0:Average 1:Sum 2:Min 3:Max
lbn PTExt TYPE_TANK TANK_EXT Pressure 3 # 0:Average 1:Sum 2:Min 3:Max
lbn PTInt TYPE_TANK TANK_INT Pressure 3 # 0:Average 1:Sum 2:Min 3:Max
#sb TYPE_DISPLAY Setting Pcur
jal check_state
jal check_display
beqal Sequence 1 close_request
beqal Sequence 2 close_door
beqal Sequence 3 depressure_ext
beqal Sequence 4 pressure_int
beqal Sequence 5 open_request
beqal Sequence 6 depressure_int
beqal Sequence 7 pressure_ext
beqal Sequence 8 open_door
j start

close_request:
bne Request 0 ra
move Sequence 2
j ra
open_request:
bne Request 1 ra
move Sequence 6
j ra
close_door:
sb TYPE_GATE_MEDIUM Open 0
sb TYPE_GATE_LARGE Open 0
beq Closed 0 ra
move Sequence 3
j ra
open_door:
sb TYPE_GATE_MEDIUM Open 1
sb TYPE_GATE_LARGE Open 1
beq Closed 1 ra
move Sequence 1
j ra
depressure_ext:
sbn TYPE_VENT VENT_EXTERNAL Mode 1
sbn TYPE_VENT VENT_EXTERNAL On 1
bne Pcur 0 ra
sbn TYPE_VENT VENT_EXTERNAL On 0
move Sequence 4
j ra
pressure_ext:
sbn TYPE_VENT VENT_EXTERNAL Mode 0
sbn TYPE_VENT VENT_EXTERNAL On 1
bge PTExt PTANK_EXT ra
sbn TYPE_VENT VENT_EXTERNAL On 0
move Sequence 8
j ra
depressure_int:
sbn TYPE_VENT VENT_INTERNAL Mode 1
sbn TYPE_VENT VENT_INTERNAL On 1
bne Pcur 0 ra
sbn TYPE_VENT VENT_INTERNAL On 0
move Sequence 7
j ra
pressure_int:
sbn TYPE_VENT VENT_INTERNAL Mode 0
sbn TYPE_VENT VENT_INTERNAL On 1
bge PTInt PTANK_INT ra
sbn TYPE_VENT VENT_INTERNAL On 0
move Sequence 5
j ra
check_state:
lb IsOpen TYPE_GATE_MEDIUM Open 3 # 0:Average 1:Sum 2:Min 3:Max
lb Temp TYPE_GATE_LARGE Open 3 # 0:Average 1:Sum 2:Min 3:Max
or IsOpen IsOpen Temp
lb IsIdle TYPE_GATE_MEDIUM Idle 2 # 0:Average 1:Sum 2:Min 3:Max
lb Temp TYPE_GATE_LARGE Idle 2 # 0:Average 1:Sum 2:Min 3:Max
or IsIdle IsIdle Temp
seq Closed IsOpen 0 # Portes ferm√©es
and Closed Closed IsIdle # Et inactives
select Temp Closed 4 2 # 2:red 4:green
sb TYPE_LED Color Temp
lb IsOxygen TYPE_SENSOR RatioOxygen 3 # 0:Average 1:Sum 2:Min 3:Max
sgt IsOxygen IsOxygen 0
j ra

check_display:
sbn TYPE_DISPLAY DISP_1 Setting PTInt
sbn TYPE_DISPLAY DISP_2 Setting PTExt
j ra