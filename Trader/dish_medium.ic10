# Trader\dish_medium_4.ic10
alias DialH d0
alias DialV d1
alias DialP d2
alias DisplayH d3
alias DisplayV d4
alias DisplayP d5
# Alias Register
alias H r0
alias V r1
alias P r2
alias Device r3
alias IsPresent r4
alias HashName r5
alias IsOn r6
alias Clock r7
alias Temp r15
# Constant
define TYPE_DISH 439026183 # medium:439026183 large:1913391845
define TYPE_COMPUTER -405593895 # retro:-405593895 new:-626563514
define TYPE_OCCUPANCY 568800213 # proxy:568800213 occupacy:322782515
define TYPE_DISPLAY -815193061
define TYPE_LANDINGPAD -2066405918
define TYPE_BUTTON 491845673
define BUTTON_ACTIVATE HASH("Button Activate")
define BUTTON_LIGHT HASH("Button Light")
define TYPE_LIGHT 1592905386
define DELAY 5
# Settings
move sp 0
move Clock 0
push HASH("Medium Satellite Dish 1")
push HASH("Medium Satellite Dish 2")
push HASH("Medium Satellite Dish 3")
push HASH("Medium Satellite Dish 4")
s DialH Mode 90
s DialV Mode 90
s DialP Mode 10
start:
yield
lb IsPresent TYPE_OCCUPANCY Activate 3 # 0:Average 1:Sum 2:Min 3:Max
sb TYPE_COMPUTER On IsPresent
sb TYPE_DISPLAY On IsPresent
sb TYPE_DISH On IsPresent
breq IsPresent 1 2
s DialP Setting 0
# move dish
l H DialH Setting
l V DialV Setting
l P DialP Setting

mul Temp P 5000
sb TYPE_DISH Setting Temp

jal move_fly
jal check_light
jal move_dish
bgtal Clock DELAY display_settings
bleal Clock DELAY display_mesure
sub Clock Clock 1
bge Clock 0 start # wait timer
mul Clock DELAY 2 # reset timer in second
j start

display_settings:
sb TYPE_DISPLAY Color 1
s DisplayH Setting H
s DisplayV Setting V
lbn Temp TYPE_DISH HashName Setting 3 # 0:Average 1:Sum 2:Min 3:Max
s DisplayP Setting Temp
j ra

display_mesure:
sb TYPE_DISPLAY Color 11
lbn Temp TYPE_DISH HashName Horizontal 3 # 0:Average 1:Sum 2:Min 3:Max
s DisplayH Setting Temp
lbn Temp TYPE_DISH HashName Vertical 3 # 0:Average 1:Sum 2:Min 3:Max
s DisplayV Setting Temp
lb Temp TYPE_DISH RequiredPower 1 # 0:Average 1:Sum 2:Min 3:Max
s DisplayP Setting Temp
j ra

move_fly:
lbn Temp TYPE_BUTTON BUTTON_ACTIVATE Setting 3 # 0:Average 1:Sum 2:Min 3:Max
beq Temp 0 ra
sb TYPE_LANDINGPAD Activate 2
j ra

check_light:
lbn Temp TYPE_BUTTON BUTTON_LIGHT Setting 3 # 0:Average 1:Sum 2:Min 3:Max
beq Temp 0 ra
lb Temp TYPE_LIGHT On 3 # 0:Average 1:Sum 2:Min 3:Max
select Temp Temp 0 1
sb TYPE_LIGHT On Temp
j ra

move_dish:
move sp 4
dish_loop:
pop HashName
mul Temp sp 90
add Temp Temp H
sbn TYPE_DISH HashName Horizontal Temp
sbn TYPE_DISH HashName Vertical V
bne sp 0 dish_loop
j ra