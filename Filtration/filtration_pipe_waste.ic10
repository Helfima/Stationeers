# filtration_pipe_waste.ic10
alias Atmo db
alias Analyser d0
alias Storage d1
# Constant
define MOLES_MIN 1
define PERCENT 0.00
define T_INPUT_MAX 200 # Degre celcius
define T_OUTPUT_MAX 40 # Degre celcius
define P_OUTPUT_MAX 6000 # kpa
define KELVIN 273
define TYPE_VALVE -1280984102
define TYPE_PUMP -321403609
# Alias Register
alias IsOn r0
alias Temp r1
alias Moles r2
alias Input r3
alias Output r4
alias IsRatio r5
alias IsCoolant r6
alias TCoolant r7
alias TInputMax r11
alias TOutputMax r12
# Settings
#s Atmo On 0 #Turn Off
start:
yield
jal check_coolant
jal check_storage
move IsOn 0
add TInputMax T_INPUT_MAX KELVIN # Degre kelvin
add TOutputMax T_OUTPUT_MAX KELVIN # Degre kelvin
l Moles Atmo TotalMolesInput
blt Moles MOLES_MIN bypass #Turn Off
l Moles Atmo TotalMolesOutput
brgt Moles 0 3 #check if empty
move IsOn 1
j bypass
l Temp Atmo TemperatureInput
bgt Temp TInputMax bypass #Turn Off
#check ratio
move IsOn 0
# check Oxygen
l Output Atmo RatioOxygenOutput
l Input Atmo RatioOxygenInput
jal check_ratio
# check Pollutant
l Output Atmo RatioPollutantOutput
l Input Atmo RatioPollutantInput
jal check_ratio
# check Volatiles
l Output Atmo RatioVolatilesOutput
l Input Atmo RatioVolatilesInput
jal check_ratio
# check CarbonDioxide
l Output Atmo RatioCarbonDioxideOutput
l Input Atmo RatioCarbonDioxideInput
jal check_ratio
# check Nitrogen
l Output Atmo RatioNitrogenOutput
l Input Atmo RatioNitrogenInput
jal check_ratio
# check NitrousOxide
l Output Atmo RatioNitrousOxideOutput
l Input Atmo RatioNitrousOxideInput
jal check_ratio
bypass:
#s db Setting 999
s Atmo Mode IsOn #Turn On/Off 0:idle 1:active
j start

check_ratio:
beq Output 0 ra
sgt IsRatio Input PERCENT
or IsOn IsOn IsRatio
j ra

check_coolant:
bdns Analyser ra #device is not set
l Temp Atmo TemperatureOutput
l TCoolant Analyser Temperature
sgt IsCoolant Temp TCoolant
sb TYPE_VALVE On IsCoolant
j ra

check_storage:
bdns Storage ra #device is not set
ls Temp Storage 0 Occupied
brne Temp 1 5 #no canister
l Temp Atmo TemperatureOutput
brgt Temp TOutputMax 3
l Temp Storage Pressure
brlt Temp P_OUTPUT_MAX 3
sb TYPE_PUMP On 0 # pump turn off
j ra
sb TYPE_PUMP On 1 # pump turn on
j ra