# Furnace\Furnace_Process.ic10
alias Mirror d0
alias PumpFuel d1
alias PumpIn d3
alias PumpOut d4
# Alias Register
alias Active r0
alias Hash r1
alias Hash1 r2
alias Hash2 r3
alias Hash3 r4
alias Qty1 r5
alias Qty2 r6
alias Qty3 r7
alias Qty r8
alias Factor r9
alias Index r10
alias FuelOn r11
alias Temp r12
alias Temp1 r13
alias Temp2 r14
alias Value r15
# Constant
define TYPE_FURNACE 545937711
define BUTTON_ID $28234
define LEVER_ID $F512
define TYPE_SENSOR -1252983604
define DIAL_INGOT_ID $F517
define DIAL_QUANTITY_ID $F518
define FACTOR 200
define QUERY_FREE 9000000
define QUERY_ITEM 9000101
define FUEL_DELAY 2
# Settings
s db Setting 0
move FuelOn 0
move Index 0
move Active 0
sd DIAL_INGOT_ID Mode 17
sd DIAL_QUANTITY_ID Mode 4
start:
yield
ld Temp BUTTON_ID Setting
beqal Temp 1 checkFuel
ld Temp DIAL_QUANTITY_ID Setting
mul Factor Temp FACTOR
ld sp DIAL_INGOT_ID Setting
jal loadValues
ld Temp LEVER_ID Setting
jal checkActive
jal checkInActive
jal checkRoom
jal request
j start

checkFuel:
select FuelOn FuelOn 0 1
s db Setting FuelOn
s PumpFuel On FuelOn
s PumpFuel Setting 5
j ra
checkActive:
beq Temp 0 ra # Lever State
bne Active 0 ra
move Active 1
j ra
checkInActive:
beq Temp 1 ra # Lever State
bne Active 2 ra
move Active 0
j ra

request:
bne Active 1 ra
lb Temp TYPE_FURNACE Reagents 1
bge Temp Factor ra
move Index 2 # Register Index
loopIndex:
move Value 0
move Hash rr10
add r8 Index 3 # Register Index
move Qty rr8
beq Qty 0 nextOre # Next si Qty=0
l Temp Mirror Setting # Read Memory Provider via mirror
yield
brne Temp QUERY_FREE -3 # Wait
s d0:0 Channel0 Hash # Write Hash
s d0:0 Channel1 Qty # Write Quantity
s d0:0 Channel2 0 # Write Rootage
s Mirror Setting QUERY_ITEM # Write Memory Provider via mirror
l Temp Mirror Setting # Read Memory Provider via mirror
brne Temp QUERY_FREE -1 # Wait
nextOre:
add Index Index 1
blt Index 5 loopIndex
move Active 2
j ra
checkRoom:
bdns d3 ra
lb Temp TYPE_SENSOR Temperature 1 # Device Sensor
sub Temp 450 Temp # Temperature
mul Temp Temp 50 # Velocity
min Temp Temp 10 # Maximum
s PumpIn Setting Temp
sgt Temp Temp 0
s PumpIn On Temp
bdns d4 ra
lb Temp TYPE_SENSOR Pressure 1 # Device Sensor
sub Temp Temp 10 # Room Pressure
mul Temp Temp 50 # Velocity
min Temp Temp 100 # Maximum
s PumpOut Setting Temp
sgt Temp Temp 0
s PumpOut On Temp
j ra
loadValues:
mul sp sp 11
move r15 7 # default 11, lecture des quantit√©s
add sp sp r15
pop rr15
sub r15 r15 1
brne r15 1 -2 # No Hash
mul Qty1 Qty1 Factor
mul Qty2 Qty2 Factor
mul Qty3 Qty3 Factor
j ra