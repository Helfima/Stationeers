# Furnace\Furnace_Furnace_PID.ic10
alias DialIngot d0
alias DialQty d1
# Alias Register
alias Process r0
alias Temp r1
alias Temp1 r2
alias Temp2 r3
alias EInlast r4
alias EInSum r5
alias EOutlast r6
alias EOutSum r7
alias Tmin r8
alias Tmax r9
alias Pmin r10
alias Pmax r11
alias T0 r12
alias P0 r13
alias Tcur r14
alias Pcur r15
define TYPE_FURNACE 545937711
define TYPE_ANALYSER 435685051
define TYPE_PUMP -321403609
define TYPE_MIXER 2104106366
define TYPE_LEVER 1220484876
define TYPE_BUTTON 491845673
define FACTOR 200
define Kp 0.5
define Ki 0.001
define Kd 10
sb TYPE_FURNACE On 1
closeAll:
move EInlast 0
move EInSum 0
move EOutlast 0
move EOutSum 0
sb TYPE_FURNACE SettingOutput 0
sb TYPE_FURNACE SettingInput 0
sb TYPE_PUMP On 0
sb TYPE_MIXER On 0
sb TYPE_FURNACE Open 0
start:
yield
lb Temp TYPE_BUTTON Setting 1 # 0:Average 1:Sum 2:Min 3:Max
brne Temp 1 3
sb TYPE_FURNACE Open 1
yield
l sp DialIngot Setting
mul sp sp 11
move r15 11
add sp sp r15
pop rr15
sub r15 r15 1
brge r15 8 -2
lb Process TYPE_LEVER Setting 1 # 0:Average 1:Sum 2:Min 3:Max
jal finally
min Tmax Tmax 3000 #Tmax Maximum
brgt Pmin 1000 2
min Pmax Pmax 2000 #Limit Pressure
add T0 Tmax Tmin
div T0 T0 2
brlt Tmax 1100 2 #Limit Temperature
add T0 Tmin 100
add P0 Pmax Pmin
div P0 P0 2
max P0 P0 1000 #P0 Minimum
min P0 P0 55000 #P0 Maximum
s db Setting T0
beq Process 0 closeAll
#Check Pipe
lb Tcur TYPE_FURNACE Temperature 1 # 0:Average 1:Sum 2:Min 3:Max
lb Pcur TYPE_ANALYSER Pressure 1 # 0:Average 1:Sum 2:Min 3:Max
slt Pcur Pcur 1000 #Pipe Pressure Max
sb TYPE_MIXER On Pcur
sub Temp Tcur T0 #cold input 1
mul Temp Temp 5 #Velocity
min Temp Temp 50 #Maximum
max Temp Temp -50 #Minimum
add Temp Temp 50
sb TYPE_MIXER Setting Temp #Mixer
#Check Furnace
lb Pcur TYPE_FURNACE Pressure 1 # 0:Average 1:Sum 2:Min 3:Max
lb Tcur TYPE_FURNACE Temperature 1 # 0:Average 1:Sum 2:Min 3:Max
move r8 6
move r10 -1
jal regulation
sb TYPE_FURNACE SettingOutput Temp
move r8 4
move r10 1
jal regulation
sb TYPE_FURNACE SettingInput Temp
j start
finally:
l Temp DialQty Setting
mul Temp1 Temp FACTOR
lb Temp TYPE_FURNACE Reagents 1
blt Temp Temp1 ra
lb Temp TYPE_FURNACE Mode 1
beq Temp 0 ra
lb Temp TYPE_FURNACE Mode 1
sb TYPE_FURNACE Open Temp
breq Temp 1 -2
sb TYPE_LEVER Open 0
j closeAll
regulation:
add r9 r8 1 #Registre suivant
sub Temp1 P0 Pcur
div Temp1 Temp1 50
mul Temp1 Temp1 r10 #Invert sign
sub Temp2 T0 Tcur
div Temp2 Temp2 10
mul Temp2 Temp2 r10 #Invert sign
brgt Temp1 Temp2 2 #Priority Pressure
move Temp1 Temp2
add rr9 rr9 Temp1 #Calcul Somme
sub Temp Temp1 rr8 #Calcul Differenciel
move rr8 Temp1
mul Temp Temp Kd #Calcul Derivee
mul Temp2 rr9 Ki #Calcul Integrale
#add Temp Temp Temp2
mul Temp2 rr8 Kp #Calcul Proportionnelle
move Temp 0
add Temp Temp Temp2
min Temp Temp 100 #Maximum
max Temp Temp 0 #Minimum
j ra