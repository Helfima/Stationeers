# Adv Furnace Temperature Controller
alias DisplayTmin d0
alias DisplayTcur d1
alias DisplayTmax d2
alias DisplayPmin d3
alias DisplayPcur d4
alias DisplayPmax d5
# Alias Register
alias Choose r0 # Dial setting
alias Hash r1
alias Hash1 r2
alias Hash2 r3
alias Hash3 r4
alias Qty1 r5
alias Qty2 r6
alias Qty3 r7
alias Tmin r8
alias Tmax r9
alias Pmin r10
alias Pmax r11
alias Tcur r12
alias Pcur r13
#Constant
define TYPE_FURNACE 545937711
define TYPE_DIAL 554524804
define FACTOR 800
#settings
s DisplayTmin On 1
s DisplayTmin Color -1
s DisplayTcur On 1
s DisplayTmax On 1
s DisplayTmax Color 4
s DisplayPmin On 1
s DisplayPmin Color -1
s DisplayPcur On 1
s DisplayPmax On 1
s DisplayPmax Color 4
start:
yield
lb Choose TYPE_DIAL Setting 1 # Read unique dial
lb Tcur TYPE_FURNACE Temperature 1
lb Pcur TYPE_FURNACE Pressure 1
floor Tcur Tcur
floor Pcur Pcur
s db Setting Choose
jal loadValues
jal setTemperatureColor
jal setPressureColor
s DisplayTmin Setting Tmin
s DisplayTmax Setting Tmax
s DisplayPmin Setting Pmin
s DisplayPmax Setting Pmax
j start
setTemperatureColor:
s DisplayTcur Setting Tcur
sgt r14 Tcur Tmin
sgt r15 Tcur Tmax
add r15 r15 r14
mul r15 r15 2
brne r15 0 2
move r15 -1 # fix color blue
s DisplayTcur Color r15 #0:Blue 2:Green 4:Red
j ra
setPressureColor:
s DisplayPcur Setting Pcur
sgt r14 Pcur Pmin
sgt r15 Pcur Pmax
add r15 r15 r14
mul r15 r15 2
brne r15 0 2
move r15 -1 # fix color blue
s DisplayPcur Color r15 #0:Blue 2:Green 4:Red
j ra
loadValues:
mul sp Choose 11
move r15 11
add sp sp r15
pop rr15
sub r15 r15 1
brne r15 0 -2
mul Qty1 Qty1 FACTOR
mul Qty2 Qty2 FACTOR
mul Qty3 Qty3 FACTOR
j ra