# Furnace\Furnace_Display_T&P.ic10
# Alias Register
alias Hash r1
alias Hash1 r2
alias Hash2 r3
alias Hash3 r4
alias Qty1 r5
alias Qty2 r6
alias Qty3 r7
alias Tmin r8
alias Tmax r9
alias Pmin r10
alias Pmax r11
alias Tcur r12
alias Pcur r13
#Constant
define TYPE_FURNACE 545937711
define TYPE_DISPLAY -815193061 # Small Display
define DIAL_INGOT_ID $19DA8
define DIAL_QUANTITY_ID $19DAA
define DISP_PMIN_ID $19C54
define DISP_PCUR_ID $19C55
define DISP_PMAX_ID $19C56
define DISP_TMIN_ID $19C51
define DISP_TCUR_ID $19C52
define DISP_TMAX_ID $19C53
define FACTOR 800
#settings
sb TYPE_DISPLAY On 1
sd DISP_PMIN_ID Color -1
sd DISP_PMAX_ID Color 4
sd DISP_TMIN_ID Color -1
sd DISP_TMAX_ID Color 4
start:
yield
ld sp DIAL_INGOT_ID Setting
s db Setting sp
lb Tcur TYPE_FURNACE Temperature 1
lb Pcur TYPE_FURNACE Pressure 1
floor Tcur Tcur
floor Pcur Pcur
jal loadValues
jal setPressureColor
jal setTemperatureColor
sd DISP_PMIN_ID Setting Pmin
sd DISP_PMAX_ID Setting Pmax
sd DISP_TMIN_ID Setting Tmin
sd DISP_TMAX_ID Setting Tmax
j start

setPressureColor:
sd DISP_PCUR_ID Setting Pcur
sgt r14 Pcur Pmin
sgt r15 Pcur Pmax
add r15 r15 r14
mul r15 r15 2
brne r15 0 2
move r15 -1 # fix color blue
sd DISP_PCUR_ID Color r15 #0:Blue 2:Green 4:Red
j ra

setTemperatureColor:
sd DISP_TCUR_ID Setting Tcur
sgt r14 Tcur Tmin
sgt r15 Tcur Tmax
add r15 r15 r14
mul r15 r15 2
brne r15 0 2
move r15 -1 # fix color blue
sd DISP_TCUR_ID Color r15 #0:Blue 2:Green 4:Red
j ra

loadValues:
mul sp sp 11
move r15 11
add sp sp r15
pop rr15
sub r15 r15 1
brne r15 0 -2
mul Qty1 Qty1 FACTOR
mul Qty2 Qty2 FACTOR
mul Qty3 Qty3 FACTOR
j ra