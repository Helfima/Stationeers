# Centrifuge Controller
alias Self d0
# Constant
define THROTTLE_MAX 100
define RPM_MAX 400
define RPM_MIN 100
define STRESS_MAX 10
define KELVIN 273
define Kp 2     # coef Proportionnel
define Ki 0.01 # coef Integrale
define Kd 0.5   # coef Derivee
define Ks 10   # coef Stress
# Alias Register
alias IsOn r0
alias MRpm r1 # mesure rpm
alias MThr r2 # mesure throttle
alias MStr r3 # mesure stress
alias Last r4 # Valeur Precedente
alias Total r5 # Valeur Somme
alias Ve r6 # Valeur Erreur
alias Vp r7 # Valeur Proportionnelle
alias Vi r8 # Valeur Integrale
alias Vd r9 # Valeur Derivee
alias VStress r10 #valeur Stress
alias CThr r11 #commande throttle
alias CCom r12 #commande combustion
alias Mes r13
# Settings
move r4 0
move r5 0
start:
yield
l MRpm Self Rpm
l MThr Self Throttle
l MStr Self Stress
mul VStress MStr Ks
jal combustion
jal throttle_pid
jal debug_display #seulement pour test
j start

throttle_pid:
sub Ve THROTTLE_MAX MThr #fonction d'erreur (consigne - mesure)
sub Ve Ve VStress # on ajoute le cap du stress
mul Vp Ve Kp # calcul Proportionnel
sub Vd Last Ve # erreur Precedente - erreur Courante
mul Vd Vd Kd # calcul Derivee
move Last Ve   # memorise l'erreur
add Total Total Ve # somme des erreurs
mul Vi Total Ki # calcul Integrale
add CThr Vp Vi
add CThr CThr Vd
s Self Throttle CThr
j ra

combustion:
brgt MRpm RPM_MIN 3
s Self CombustionLimiter 0
j ra
sub CCom THROTTLE_MAX VStress
s Self CombustionLimiter CCom
j ra

# Optionnel: affecte les valeur dans des memoires pour les graphes
debug_display:
brdns d1 2
s d1 Setting Ve
brdns d2 2
s d2 Setting Vp
brdns d2 2
s d3 Setting Vd
brdns d4 2
s d4 Setting Vi
brdns d5 2
s d5 Setting CThr
j ra