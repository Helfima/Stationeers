# Machine\machine_controller_v3.ic10
alias Machine d0
# Alias Register
alias IsActivate r0
alias IsOccupied r1
alias IsTrigger r2
alias IsRunning r3
alias Completion r4
alias HashMachine r5
alias HashStacker r6
alias Qty r7
alias Timer r8
alias Dial r9
alias Stack r10
#Constant
define TYPE_DIAL 554524804
define TYPE_LIGHT -1535893860
define TYPE_STACKER 1585641623 # StructureStackerReverse
define TYPE_DISPLAY -815193061 # StructureConsoleLED5
define DISP_MACHINE HASH("Display Machine")
define DISP_STACKER HASH("Display Stacker")
define TYPE_MEMORY -851746783 # StructureLogicMemory
define MEMO_MACHINE HASH("Memory Machine")
define MEMO_STACKER HASH("Memory Stacker")
define TYPE_TRIGGER 1269458680 # Medium:1269458680 large:-2008706143
define TYPE_MIRROR 2096189278 # Mirror link Transformer
define DELAY 30
#Settings
sb TYPE_DIAL Mode 5
sb TYPE_DISPLAY On 1
sbn TYPE_DISPLAY DISP_MACHINE Color 5
sbn TYPE_DISPLAY DISP_STACKER Color 10
sb TYPE_STACKER On 1
sb TYPE_STACKER Setting 50
move IsRunning 0
move Stack 0
start:
yield
s db Setting IsTrigger
jal checkTriggerPlate
beq IsRunning 0 start
l Completion Machine CompletionRatio
l HashMachine Machine RecipeHash
l IsActivate Machine Activate
lbs Qty TYPE_STACKER 2 Quantity 3 # 0:Average 1:Sum 2:Min 3:Max
lbs HashStacker TYPE_STACKER 2 OccupantHash 3 # 0:Average 1:Sum 2:Min 3:Max
lbs IsOccupied TYPE_STACKER 1 Occupied 3 # 0:Average 1:Sum 2:Min 3:Max
lb Dial TYPE_DIAL Setting 1 # Read unique dial
# Light
sb TYPE_LIGHT On IsActivate
# Display Machine
sbn TYPE_DISPLAY DISP_MACHINE Mode IsActivate
sbn TYPE_DISPLAY DISP_MACHINE Setting Completion
sbn TYPE_MEMORY MEMO_MACHINE Setting HashMachine
# Display Stacker
sbn TYPE_DISPLAY DISP_STACKER Setting Qty
sbn TYPE_MEMORY MEMO_STACKER Setting HashStacker
jal timerOff
jal checkStack
j start

timerOff:
beq IsActivate 1 ra
sbn TYPE_DISPLAY DISP_MACHINE Setting Timer
blt Timer 0 ra
sub Timer Timer 1
bge Timer 0 ra
sb TYPE_STACKER Activate 1 # Clear stacker
sb TYPE_MIRROR On 0
j ra

checkTriggerPlate:
lb IsTrigger TYPE_TRIGGER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
bne IsTrigger 1 ra
sb TYPE_MIRROR On 1
move IsRunning 1
move Timer DELAY
j ra

checkStack:
brne IsActivate 0 2
sb TYPE_STACKER ClearMemory 1
beq Dial 0 ra # Continious Mode
bne IsOccupied 0 ra
lb Stack TYPE_STACKER ExportCount 3 # 0:Average 1:Sum 2:Min 3:Max
blt Stack Dial ra
s Machine Activate 0 # Stop machine
j ra