# Dish Controller
alias ButtonAuto d0
alias ButtonContinue d1
alias Computer d2
alias DisplayH d3
alias DisplaySkip d5
# Constant
define TYPE_DISH 439026183
define TYPE_FLASH -1535893860
# Alias Register
alias H r0 # Horizontal
alias DeltaH r1 # Horizontal
alias Signal r4
alias Strength r5
alias Temp r10
alias IsAuto r11
alias IsReset r12
alias IsContinue r13
alias IsSkip r14
alias Skip r15
# Settings
move H 0
move DeltaH 5
move Strength 0
move Skip 0
start:
sb TYPE_FLASH On 0
yield
jal updateDisplay
l IsAuto ButtonAuto Setting
l IsContinue ButtonContinue Setting
ls IsReset Computer 1 Occupied
beqal IsReset 0 reset
beq IsAuto 1 auto
beq IsContinue 1 continue
j start
reset:
move Skip 0
j ra
continue:
sub Skip Skip 1
auto:
sb TYPE_FLASH On 1
move Strength 0
loop:
sb TYPE_DISH Horizontal H
yield
loopH:
lb Strength TYPE_DISH SignalStrength 3 # 0:Average 1:Sum 2:Min 3:Max
lb Signal TYPE_DISH SignalID 3 # 0:Average 1:Sum 2:Min 3:Max
jal checkSkip
breq IsSkip 1 2
bge Strength 0.95 loopEnd
lb Temp TYPE_DISH Horizontal 0 # 0:Average 1:Sum 2:Min 3:Max
jal updateDisplay
brgt Temp 1 2
jr 2
blt Temp 359 loopH
breq H 0 3
move H 0
jr 2
move H 360
yield
j loop
loopEnd:
jal addSkip
lb Temp TYPE_DISH Horizontal 0 # 0:Average 1:Sum 2:Min 3:Max
yield
sb TYPE_DISH Horizontal Temp
j start
checkSkip:
beq Skip 0 ra
move IsSkip 0
move sp Skip
ble sp 0 ra # End stack
pop Temp
brne Temp Signal -2
move IsSkip 1
j ra
addSkip:
move sp Skip
push Signal
add Skip Skip 1
j ra
updateDisplay:
s DisplayH Setting H
s DisplaySkip Setting Skip
j ra