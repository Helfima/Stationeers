# Atmo Controller
alias AnalyserGas d0
alias AtmoWaste d1
alias AtmoExt d2
alias AnalyserWaste d3
alias AnalyserCoolant d4
alias ValveCoolant d5
# Constant
define KELVIN 273
define MOLES 1
define TMAX -10 # Degre celcius
define PMAX 5000
# Alias Register
alias nWaste r0
alias ratio r1
alias Tgas r2
alias Pgas r3
alias Tcoolant r4
alias Tmax r5
alias IsCoolant r13
alias NeedCoolant r14
alias Temp r15
start:
yield
add Tmax TMAX KELVIN # Degre kelvin
l nWaste AnalyserWaste TotalMoles
l Tgas AnalyserGas Temperature
l Pgas AnalyserGas Pressure
l Tcoolant AnalyserCoolant Temperature
jal readRatio
jal checkCoolant
jal checkFiltration
jal checkFiltrationExt
j start

checkFiltration:
bdns AtmoWaste ra
mul Temp nWaste ratio
s db Setting ratio
yield
s db Setting Temp
sgt Temp Temp MOLES
s AtmoWaste On Temp
j ra

checkFiltrationExt:
bdns AtmoExt ra
sgt Temp PMAX Pgas
s AtmoExt On Temp
j ra

checkCoolant:
bdns ValveCoolant ra
slt IsCoolant Tcoolant Tgas
slt NeedCoolant Tmax Tgas
and Temp IsCoolant NeedCoolant
s ValveCoolant On Temp
j ra

readRatio:
move r10 11
l ratio AnalyserGas RatioOxygen
brgtz ratio r10
l ratio AnalyserGas RatioCarbonDioxide
brgtz ratio r10
l ratio AnalyserGas RatioVolatiles
brgtz ratio r10
l ratio AnalyserGas RatioNitrogen
brgtz ratio r10
l ratio AnalyserGas RatioNitrousOxide
brgtz ratio r10
l ratio AnalyserGas RatioPollutant
jr r10
l ratio AnalyserWaste RatioOxygen
j ra
l ratio AnalyserWaste RatioCarbonDioxide
j ra
l ratio AnalyserWaste RatioVolatiles
j ra
l ratio AnalyserWaste RatioNitrogen
j ra
l ratio AnalyserWaste RatioNitrousOxide
j ra
l ratio AnalyserWaste RatioPollutant
j ra