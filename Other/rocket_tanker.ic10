# Rocket Controller
alias DisplayState d0
alias DisplayFuel d1
alias DisplayCollectable d2
alias DisplayQuantity d3
# Constant
define TYPE_AUTOMATION 1556756312
define TYPE_SILO 906210602
define TYPE_TANK -505333199
define TYPE_MINER_ORE -2027419821
define TYPE_MINER_GAS -1969723301
define TYPE_MINER_ICE 74521920
define TYPE_BUTTON 491845673
define TYPE_PUMP -321403609
define TYPE_PUMP_TURBO 1310794736
define FUEL_MAX 8000
define FUEL_MIN 600
define P_MAX 20000
# Alias Register
alias State r0
alias ValFuel r1
alias Qty r2
alias Collectable r3
alias Fire r4
alias IsWorking r6
alias CheckState r9
alias CheckFuel r10
alias Temp r11
start:
yield
lb Fire TYPE_BUTTON Setting 1
lb State TYPE_AUTOMATION Mode 1
lb ValFuel TYPE_AUTOMATION Fuel 1
s db Setting State
jal checkTank
s DisplayState Setting State
s DisplayFuel Setting ValFuel
s DisplayCollectable Setting Collectable
s DisplayQuantity Setting Qty

jal checkFuel
jal loadFuel
jal unload
jal mining
jal nextArea
beq Fire 0 start
bne State 0 back
sb TYPE_AUTOMATION Activate 1 # Launching
j start
back:
sb TYPE_AUTOMATION Activate 5 # ReturningToLaunch
j start

checkTank:
lb Qty TYPE_TANK Pressure 3 # 0:Average 1:Sum 2:Min 3:Max
bgt State 4 ra
bgt Qty P_MAX back
j ra

loadFuel:
seq CheckState State 0
slt CheckFuel ValFuel FUEL_MAX
and Temp CheckState CheckFuel
sb TYPE_PUMP On Temp
j ra

unload:
lb Temp TYPE_TANK Pressure 2 # 0:Average 1:Sum 2:Min 3:Max
seq CheckState State 0
sne CheckFuel Temp 0
and Temp CheckState CheckFuel
sb TYPE_TANK Open Temp # Open Tank
sb TYPE_PUMP_TURBO On Temp # Active Pump
j ra

checkFuel:
slt CheckFuel ValFuel FUEL_MIN # Check come back
seq CheckState State 4 # Check Working
and Temp CheckState CheckFuel
beq Temp 1 back # Must Come Back
j ra

mining:
bne State 4 ra # Do Nothing
lb Collectable TYPE_AUTOMATION CollectableGoods 1
sb TYPE_MINER_ORE Activate Collectable # Mining
sb TYPE_MINER_GAS Activate Collectable # Mining
sb TYPE_MINER_ICE Activate Collectable # Mining
j ra

nextArea:
bne State 4 ra # Do Nothing
beq Collectable 1 ra # Do Nothing
s db Setting 40
sb TYPE_AUTOMATION Activate 4 # Idle
yield
sb TYPE_AUTOMATION Activate 2 # Traveling
j ra