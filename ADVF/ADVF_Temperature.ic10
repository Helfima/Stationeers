# Adv Furnace Temperature Controller
alias DisplayTmin d0
alias DisplayTcur d1
alias DisplayTmax d2
alias GasInput d3
alias Manual2 d4
# Alias Register
alias Stage r0 # Current Stage
alias Choose r1 # Dial setting
alias Tmin r2
alias Tcur r3
alias Tmax r4
#Constant
define TYPE_FURNACE 545937711
define TYPE_DIAL 554524804
define TYPE_MEMORY -851746783
#settings
s DisplayTmin On 1
s DisplayTmin Color 0
s DisplayTcur On 1
s DisplayTmax On 1
s DisplayTmax Color 4
start:
yield
lb Stage TYPE_MEMORY Setting 1 # Read unique memory
lb Choose TYPE_DIAL Setting 1 # Read unique dial
lb Tcur TYPE_FURNACE Temperature 1
s db Setting Choose
jal getPressure
jal setColor
s DisplayTmin Setting Tmin
s DisplayTmax Setting Tmax
beq Stage 5 request # Stage 5
j start
request:
jal setColor
lb Tcur TYPE_FURNACE Temperature 1
blt Tcur Tmin failure
blt Tcur Tmax nextStage
s GasInput Setting 2 # N2 Gas
s Manual2 Setting 2 # Manual2
j request
nextStage:
s GasInput Setting 0 # Stop Gas
s Manual2 Setting 0 # Manual2
add Stage Stage 1
sb TYPE_MEMORY Setting Stage
j start
failure:
s GasInput Setting 0 # Stop Gas
s Manual2 Setting 0 # Manual2
sb TYPE_MEMORY Setting 15
j start

setColor:
s DisplayTcur Setting Tcur
brgt Tcur Tmax 6
brgt Tcur Tmin 3
s DisplayTcur Color 0 #Blue
j ra
s DisplayTcur Color 2 #Green
j ra
s DisplayTcur Color 4 #Red
j ra

getPressure:
move Tmin 600 # Default
move Tmax 99999 # Default
brne Choose 0 2 # Lead Ingot
move Tmin 400
brne Choose 1 2 # Charcoal Ingot
move Tmin 580
brne Choose 5 2 # Iron Ingot
move Tmin 800
brne Choose 6 2 # Nickel Ingot
move Tmin 800
brne Choose 7 2 # Silicon Ingot
move Tmin 900
brne Choose 8 2 # Steel Ingot
move Tmin 900
brne Choose 10 3 # Solder Ingot
move Tmin 350
move Tmax 550
brne Choose 11 3 # Constantan Ingot
move Tmin 1000
move Tmax 10000
brne Choose 12 3 # Invar Ingot
move Tmin 1200
move Tmax 1500
brne Choose 13 2 # Astroloy Ingot
move Tmin 1000
brne Choose 14 2 # Inconel Ingot
move Tmin 600
brne Choose 15 3 # Waspaloy Ingot
move Tmin 400
move Tmax 800
brne Choose 16 3 # Hastelloy Ingot
move Tmin 950
move Tmax 1000
brne Choose 17 2 # Stellite Ingot
move Tmin 1800
j ra