# Adv Furnace Pressure Controller
alias DisplayPmin d0
alias DisplayPcur d1
alias DisplayPmax d2
alias Manual1 d3
# Alias Register
alias Stage r0 # Current Stage
alias Choose r1 # Dial setting
alias Pmin r2
alias Pcur r3
alias Pmax r4
#Constant
define TYPE_FURNACE 545937711
define TYPE_DIAL 554524804
define TYPE_MEMORY -851746783
#settings
s DisplayPmin On 1
s DisplayPmin Color 0
s DisplayPcur On 1
s DisplayPmax On 1
s DisplayPmax Color 4
start:
yield
lb Stage TYPE_MEMORY Setting 1 # Read unique memory
lb Choose TYPE_DIAL Setting 1 # Read unique dial
lb Pcur TYPE_FURNACE Pressure 1
s db Setting Choose
jal getValues
jal setColor
s DisplayPmin Setting Pmin
s DisplayPmax Setting Pmax
beq Stage 6 request # Stage 6
j start
request:
jal setColor
lb Pcur TYPE_FURNACE Pressure 1
blt Pcur Pmin failure
blt Pcur Pmax nextStage
purge:
jal setColor
lb Pcur TYPE_FURNACE Pressure 1
sb TYPE_FURNACE SettingOutput 10
s Manual1 Setting 1 # Manual1
bgt Pcur Pmax purge
sb TYPE_FURNACE SettingOutput 0
s Manual1 Setting 0 # Manual1
sb TYPE_MEMORY Setting 5 # Recheck Temperature
j start
nextStage:
add Stage Stage 1
sb TYPE_MEMORY Setting Stage
j start
failure:
sb TYPE_MEMORY Setting 16
j start

setColor:
s DisplayPcur Setting Pcur
brgt Pcur Pmax 6
brgt Pcur Pmin 3
s DisplayPcur Color 0 #Blue
j ra
s DisplayPcur Color 2 #Green
j ra
s DisplayPcur Color 4 #Red
j ra

getValues:
move Pmin 100 # Default
move Pmax 99999 # Default
blt Choose 8 ra # Bypass
brne Choose 8 2 # Steel Ingot
move Pmin 1000
brne Choose 9 3 # Electrum Ingot
move Pmin 800
move Pmax 2400
brne Choose 10 2 # Solder Ingot
move Pmin 1000
brne Choose 11 2 # Constantan Ingot
move Pmin 20000
brne Choose 12 3 # Invar Ingot
move Pmin 18000
move Pmax 20000
brne Choose 13 3 # Astroloy Ingot
move Pmin 30000
move Pmax 40000
brne Choose 14 3 # Inconel Ingot
move Pmin 23500
move Pmax 24000
brne Choose 15 2 # Waspaloy Ingot
move Pmin 50000
brne Choose 16 3 # Hastelloy Ingot
move Pmin 25000
move Pmax 30000
brne Choose 17 3 # Stellite Ingot
move Pmin 10000
move Pmax 20000
j ra