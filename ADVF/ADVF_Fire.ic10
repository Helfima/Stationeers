# Adv Furnace Fire Controller
alias DisplayFuel d0
alias DisplayPurge d1
alias GasInput d2
alias Manual1 d3
alias Manual2 d4
# Alias Register
alias Stage r0 # Current Stage
alias Choose r1 # Dial setting
alias PFuel r2 # Pressure Fuel
alias PPurge r3 # Pressure Purge
# Constant
define TYPE_FURNACE 545937711
define TYPE_DIAL 554524804
define TYPE_MEMORY -851746783
# Settings
s DisplayFuel Color 11
s DisplayPurge Color 11
start:
yield
lb Stage TYPE_MEMORY Setting 1 # Read unique memory
lb Choose TYPE_DIAL Setting 1 # Read unique dial
jal getValues
s DisplayFuel Setting PFuel
s DisplayPurge Setting PPurge
ble Stage 0 start # normal loop
beq Stage 1 normalFire # Fire for Ore
bgt Stage 10 otherFire # Fire must return
j start
otherFire:
move r4 PFuel
move r5 PPurge
j purge
normalFire:
breq Choose 14 8 # Special Inconel
breq Choose 15 7 # Special Waspaloy
breq Choose 16 6 # Special Hastelloy
lb r6 TYPE_FURNACE Temperature 1
bgt r6 1200 nextStage # Bypass Fire
move r4 200
move r5 5
jr 3
move r4 PFuel
move r5 PPurge
purge:
jal checkExit
s DisplayPurge Setting r5
s DisplayPurge Color 4
lb r8 TYPE_FURNACE Pressure 1 # Current Pressure
sb TYPE_FURNACE SettingOutput 100 # Purge On
s Manual1 Setting 2 # Manual1
lb r7 TYPE_FURNACE Temperature 1
brgt r7 500 4 # Target Temperature
s GasInput Setting 0 # N2 Gas
s Manual2 Setting 0 # Manual2
jr 3
s GasInput Setting -50 # N2 Gas
s Manual2 Setting 2 # Manual2
bgt r8 r5 purge
sb TYPE_FURNACE SettingOutput 0 # Purge Off
s Manual1 Setting 0 # Manual1
s GasInput Setting 0 # N2 Gas
s Manual2 Setting 0 # Manual2
s DisplayPurge Color 11
fuel:
jal checkExit
s DisplayFuel Setting r4
s DisplayFuel Color 4
lb r8 TYPE_FURNACE Pressure 1 # Current Pressure
sub r9 r4 r8 #Delta PFuel
max r9 r9 1 # Min 1
s GasInput Setting r9 # Fuel Gas
s Manual2 Setting 1 # Manual2
blt r8 r4 fuel # Loop Fuel
s GasInput Setting 0 # Stop Gas
s Manual2 Setting 0 # Manual2
s DisplayFuel Color 11
sb TYPE_FURNACE Activate 1 # Furnace Fire
bgt Stage 10 returnStage
nextStage:
add Stage Stage 1
sb TYPE_MEMORY Setting Stage
j start
returnStage:
sleep 1
sb TYPE_MEMORY Setting 5
j start

checkExit:
lb Stage TYPE_MEMORY Setting 1 # Read unique memory
bne Stage 0 ra
s Manual2 Setting 0 # Manual2
s Manual1 Setting 0 # Manual1
s db Setting -1
yield
j start

getValues:
move PFuel 100 # Default
move PPurge 5 # Default
blt Choose 11 ra # Bypass
brne Choose 11 2 # Constantan Ingot
move PFuel 1000
brne Choose 12 2 # Invar Ingot
move PFuel 1000
brne Choose 13 2 # Astroloy Ingot
move PFuel 1000
brne Choose 14 2 # Inconel Ingot
move PFuel 950
brne Choose 15 2 # Waspaloy Ingot
move PFuel 1600
brne Choose 16 2 # Hastelloy Ingot
move PFuel 900
brne Choose 17 2 # Stellite Ingot
move PFuel 500
j ra