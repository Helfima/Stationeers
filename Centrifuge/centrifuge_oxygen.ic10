# centrifuge_oxygen.ic10
alias Self db
alias DisplayQty d0 # Optionnal Display Reagents QTY
# Constant
define TYPE_LEVER 1220484876
define STRESS_MAX 30
define QTY_MAX 2000 # Quantity for Open
define Kpc 0.22
define Kps 0.75
define Kd 30
define Ki 0.01
# Alias Register
alias MThr r0 # mesure Throttle
alias MRpm r1 # mesure Rpm
alias LRpm r2 # valeur Last Rpm
alias DRpm r3 # valeur Difference
alias MStr r4 # mesure Stress
alias LStr r5 # valeur Last Stress
alias DStr r6 # valeur Difference
alias Err r7
alias CThr r8 # commande throttle
alias CCom r9 # commande combustion
alias Count r10
alias QTY r11
alias IsOn r12
alias IsOpen r13
alias IStr r14
alias Temp r15
# Settings
s Self Open 0
stop:
move IsOpen 0
move CThr 0
move CCom 0
move LRpm 0
move LStr 0
move IStr 0
s Self Throttle 0
s Self CombustionLimiter 0
start:
yield
lb IsOn TYPE_LEVER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
l MRpm Self Rpm
l MThr Self Throttle
l MStr Self Stress
sub DRpm MRpm LRpm
move LRpm MRpm
sub DStr MStr LStr
move LStr MStr
jal check_reagents
jal check_emptying
beq IsOn 0 stop
jal check_throttle
jal check_combustion
j start
check_emptying:
brlt QTY QTY_MAX 2
s Self Open 1
brgt QTY 10 2
s Self Open 0
l IsOpen Self Open
beq IsOpen 1 stop
j ra
check_throttle:
move CThr 0
sub Err STRESS_MAX MStr
mul CThr Err Kps
add IStr IStr Err
mul Temp IStr Ki
add CThr CThr Temp
mul Temp DStr Kd
sub CThr CThr Temp
s Self Throttle CThr
j ra
check_combustion:
mul CCom MRpm Kpc
s Self CombustionLimiter CCom
j ra

check_reagents:
move QTY 0
lr Count Self Contents HASH("Hydrocarbon")
add QTY QTY Count
lr Count Self Contents HASH("Cobalt")
add QTY QTY Count
lr Count Self Contents HASH("Copper")
add QTY QTY Count
lr Count Self Contents HASH("Gold")
add QTY QTY Count
lr Count Self Contents HASH("Iron")
add QTY QTY Count
lr Count Self Contents HASH("Lead")
add QTY QTY Count
lr Count Self Contents HASH("Nickel")
add QTY QTY Count
lr Count Self Contents HASH("Silicon")
add QTY QTY Count
lr Count Self Contents HASH("Silver")
add QTY QTY Count
lr Count Self Contents HASH("Uranium")
add QTY QTY Count
lr Count Self Contents HASH("Steel")
add QTY QTY Count
bdns DisplayQty ra
s DisplayQty Setting QTY
j ra