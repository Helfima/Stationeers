# centrifuge_display.ic10
alias Self d0
# Constant
define TYPE_LEVER 1220484876
define TYPE_MONITOR 801677497
define TYPE_DISPLAY -815193061 # Small Display
define DISP_REAGENTS HASH("Display Reagents")
define DISP_FLOW HASH("Display Reagents Flow")
define DISP_CLOCK HASH("Display Clock")
define TYPE_MEMORY -851746783
define MEMO_STRESS HASH("Memory Stress")
define MEMO_RPM HASH("Memory RPM")
define MEMO_THROTTLE HASH("Memory Throttle")
define MEMO_DIFF_RPM HASH("Memory Diff RPM")
define MEMO_DIFF_STRESS HASH("Memory Diff Stress")
define TYPE_PROXIMITY 568800213
# Alias Register
alias MThr r0 # mesure Throttle
alias MRpm r1 # mesure Rpm
alias LRpm r2 # valeur Last Rpm
alias DRpm r3 # valeur Difference
alias MStr r4 # mesure Stress
alias LStr r5 # valeur Last Stress
alias DStr r6 # valeur Difference
alias FQTY r8 # commande throttle
alias Tick r9 # commande combustion
alias Count r10
alias QTY r11
alias IsOn r12
alias IsOpen r13
alias Time r14
alias Temp r15
# Settings
stop:
move IsOpen 0
move FQTY 0
move LRpm 0
move LStr 0
move Tick 0
move Time 0
start:
yield
s db Setting 0
lb IsOn TYPE_LEVER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
l MRpm Self Rpm
l MThr Self Throttle
l MStr Self Stress
l IsOpen Self Open
select Tick IsOpen 0 Tick
select Time IsOpen 0 Time
add Tick Tick 1
add Time Time 1
s db Setting Tick
sub DRpm MRpm LRpm
move LRpm MRpm
sub DStr MStr LStr
move LStr MStr
jal reagents
jal flowQuantity
jal display
beq IsOn 0 stop
j start

flowQuantity:
div FQTY QTY Tick
mul FQTY FQTY 2
j ra

display:
sbn TYPE_MEMORY MEMO_RPM Setting MRpm
sbn TYPE_MEMORY MEMO_STRESS Setting MStr
sbn TYPE_MEMORY MEMO_THROTTLE Setting MThr
sbn TYPE_MEMORY MEMO_DIFF_RPM Setting DRpm
sbn TYPE_MEMORY MEMO_DIFF_STRESS Setting DStr
sbn TYPE_DISPLAY DISP_REAGENTS Color 10
sbn TYPE_DISPLAY DISP_REAGENTS Setting QTY
sbn TYPE_DISPLAY DISP_FLOW Color 10
sbn TYPE_DISPLAY DISP_FLOW Setting FQTY
div Temp Time 2
trunc Temp Temp
sbn TYPE_DISPLAY DISP_CLOCK Color 1
sbn TYPE_DISPLAY DISP_CLOCK Setting Temp
sb TYPE_PROXIMITY Setting 5
lb Temp TYPE_PROXIMITY Activate 3 # 0:Average 1:Sum 2:Min 3:Max
sb TYPE_DISPLAY On Temp
sb TYPE_MONITOR On Temp
j ra

reagents:
move QTY 0
lr Count Self Contents HASH("Hydrocarbon")
add QTY QTY Count
lr Count Self Contents HASH("Cobalt")
add QTY QTY Count
lr Count Self Contents HASH("Copper")
add QTY QTY Count
lr Count Self Contents HASH("Gold")
add QTY QTY Count
lr Count Self Contents HASH("Iron")
add QTY QTY Count
lr Count Self Contents HASH("Lead")
add QTY QTY Count
lr Count Self Contents HASH("Nickel")
add QTY QTY Count
lr Count Self Contents HASH("Silicon")
add QTY QTY Count
lr Count Self Contents HASH("Silver")
add QTY QTY Count
lr Count Self Contents HASH("Uranium")
add QTY QTY Count
lr Count Self Contents HASH("Steel")
add QTY QTY Count
j ra