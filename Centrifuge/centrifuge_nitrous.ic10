# Centrifuge Controller
alias Self db
alias Display d0 # Optionnal Display Reagents QTY
# Constant
define TYPE_LEVER 1220484876
define RPM_MAX 1200
define RPM_MID 500 # begin combustion
define RPM_MIN 150
define STRESS_MAX 50
define QTY_MAX 2000 # Quantity for Open
define Kr 110
define Kc 100
define Ks 180
define Kd 20
# Alias Register
alias MThr r0 # mesure Throttle
alias MRpm r1 # mesure Rpm
alias LRpm r2 # valeur Last Rpm
alias DRpm r3 # valeur Difference
alias MStr r4 # mesure Stress
alias LStr r5 # valeur Last Stress
alias DStr r6 # valeur Difference
alias Err r7
alias CThr r8 # commande throttle
alias CCom r9 # commande combustion
alias Count r10
alias QTY r11
alias IsOn r12
alias IsOpen r13
alias Temp r15
# Settings
s Self Open 0
stop:
move IsOpen 0
move CThr 0
move CCom 0
move LRpm 0
move LStr 0
s Self Throttle 0
s Self CombustionLimiter 0
start:
yield
lb IsOn TYPE_LEVER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
l MRpm Self Rpm
l MThr Self Throttle
l MStr Self Stress
sub DRpm MRpm LRpm
move LRpm MRpm
sub DStr MStr LStr
move LStr MStr
jal reagents
jal emptying
beq IsOn 0 stop
jal throttle
jal combustion
j start
emptying:
brlt QTY QTY_MAX 2
s Self Open 1
brgt QTY 10 2
s Self Open 0
l IsOpen Self Open
beq IsOpen 1 stop
j ra
throttle:
move CThr 0
add Err MRpm RPM_MIN
div Err Err RPM_MAX # fonction du rpm
mul Err Err Kr
brgt MStr STRESS_MAX 2 # bypass trop de stress
add CThr CThr Err
div Err MStr 100 # compensation du stress
mul Err Err Ks
sub CThr CThr Err
mul Err DRpm Kd # compensation ralentissement
brlt MRpm RPM_MIN 2 # bypass si rpm bas
sub CThr CThr Err
s Self Throttle CThr
j ra
combustion:
sub Err MRpm RPM_MID
sub Temp RPM_MAX RPM_MID
div Err Err Temp # fonction du rpm
mul CCom Err Kc
s Self CombustionLimiter CCom
j ra

reagents:
move QTY 0
lr Count Self Contents Hydrocarbon
add QTY QTY Count
lr Count Self Contents Cobalt
add QTY QTY Count
lr Count Self Contents Copper
add QTY QTY Count
lr Count Self Contents Gold
add QTY QTY Count
lr Count Self Contents Iron
add QTY QTY Count
lr Count Self Contents Lead
add QTY QTY Count
lr Count Self Contents Nickel
add QTY QTY Count
lr Count Self Contents Silicon
add QTY QTY Count
lr Count Self Contents Silver
add QTY QTY Count
lr Count Self Contents Uranium
add QTY QTY Count
bdns d0 ra
s d0 Setting QTY
j ra