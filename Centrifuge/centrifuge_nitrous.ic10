# Centrifuge\centrifuge_nitrous.ic10
alias Self db
alias DisplayQty d0 # Optionnal Display Reagents QTY
# Constant
define TYPE_LEVER 1220484876
define RMP_C 200
define RMP_I 50
define TIME_THR 15
define STRESS_MAX 30
define QTY_MAX 2000 # Quantity for Open
define Kpc 0.13 # Default: 0.13
define Kps 0.75 # Default: 0.75
define Kd 30    # Default: 30
define Ki 0.01  # Default: 0.01
# Alias Register
alias MThr r0 # mesure Throttle
alias MRpm r1 # mesure Rpm
alias LRpm r2 # valeur Last Rpm
alias DRpm r3 # valeur Difference
alias MStr r4 # mesure Stress
alias LStr r5 # valeur Last Stress
alias DStr r6 # valeur Difference
alias IStr r7 # valeur Integrale
alias Err r8
alias Count r8
alias CThr r9 # commande throttle
alias CCom r10 # commande combustion
alias QTY r11
alias IsOn r12
alias IsOpen r13
alias IsAction r14
alias Time r15
# Settings
s Self Open 0
stop:
s Self Throttle 0
s Self CombustionLimiter 0
initialize:
move IsAction 0
move IsOpen 0
move CThr 0
move CCom 0
move LRpm 0
move LStr 0
move IStr 0
start:
yield
lb IsAction TYPE_LEVER Setting 3 # 0:Average 1:Sum 2:Min 3:Max
l MRpm Self Rpm
l MThr Self Throttle
l MStr Self Stress
sub DRpm MRpm LRpm
move LRpm MRpm
sub DStr MStr LStr
move LStr MStr
jal reagents
jal open
beq IsAction 0 stop
jal throttle
jal combustion
j start

open:
brlt QTY QTY_MAX 2
s Self Open 1
brgt QTY 0 2
s Self Open 0
l IsOpen Self Open
breq IsOpen 1 3
move Time TIME_THR
j ra
s Self Throttle Time
s Self CombustionLimiter 0
sub Time Time 1
j initialize

throttle:
move CThr 0
sub Err STRESS_MAX MStr # calcul de l'erreur
mul CThr Err Kps # U(t)=Up Proportionnelle
add IStr IStr Err # SUM(Err) Somme des erreurs
brgt MRpm RMP_I 2 # vitesse minimale
move IStr 0 # skip integrale
mul Err IStr Ki # Ui Intégrale
add CThr CThr Err # U(t)=Up+Ui
mul Err DStr Kd # Ud dérivée
sub CThr CThr Err # U(t)=Up+Ui+Ud
s Self Throttle CThr
bdns d1 ra
s d1 Setting CThr
j ra

combustion:
sub Err MRpm RMP_C
mul CCom Err Kpc
s Self CombustionLimiter CCom
j ra

reagents:
move QTY 0
lr Count Self Contents HASH("Hydrocarbon")
add QTY QTY Count
lr Count Self Contents HASH("Cobalt")
add QTY QTY Count
lr Count Self Contents HASH("Copper")
add QTY QTY Count
lr Count Self Contents HASH("Gold")
add QTY QTY Count
lr Count Self Contents HASH("Iron")
add QTY QTY Count
lr Count Self Contents HASH("Lead")
add QTY QTY Count
lr Count Self Contents HASH("Nickel")
add QTY QTY Count
lr Count Self Contents HASH("Silicon")
add QTY QTY Count
lr Count Self Contents HASH("Silver")
add QTY QTY Count
lr Count Self Contents HASH("Uranium")
add QTY QTY Count
lr Count Self Contents HASH("Steel")
add QTY QTY Count
bdns DisplayQty ra
s DisplayQty Settings QTY
j ra